(define (domain imv_temporal)
  (:requirements :strips :typing :durative-actions)
  (:types location count obj)
  (:predicates
    (at ?o - obj ?l - location)
    (connected ?l1 ?l2 - location)
    (holding ?a - obj ?i - obj)
    (load ?a - obj ?c - count)
    (next ?c1 ?c2 - count)
    (capacity-ok ?a - obj ?c - count)
    (inside ?art - obj ?p - obj)
    (empty ?p - obj)
    (transportable ?i - obj)
    (fragile ?i - obj)
    (safe-location ?l - location)
    (ready ?a - obj)
    (movement-allowed ?a - obj)
    (seismic-safe ?l - location)
    (has-cooling ?a - obj)
    (needs-cooling ?i - obj)
  )

  (:durative-action move
    :parameters (?a - obj ?from - location ?to - location)
    :duration (= ?duration 10)
    :condition (and
        (at start (ready ?a))
        (at start (at ?a ?from))
        (over all (connected ?from ?to))
        (over all (movement-allowed ?a))
    )
    :effect (and
        (at start (not (ready ?a)))
        (at end (ready ?a))
        (at start (not (at ?a ?from)))
        (at end (at ?a ?to))
    )
  )

  (:durative-action pick_up_secure
    :parameters (?a - obj ?i - obj ?l - location ?c_pre - count ?c_post - count)
    :duration (= ?duration 5)
    :condition (and
        (at start (ready ?a))
        (at start (at ?a ?l))
        (over all (at ?a ?l))
        (at start (at ?i ?l))
        (at start (load ?a ?c_pre))
        (over all (next ?c_pre ?c_post))
        (over all (capacity-ok ?a ?c_post))
        (at start (safe-location ?l))
        (over all (transportable ?i))
        (over all (has-cooling ?a))
    )
    :effect (and
        (at start (not (ready ?a)))
        (at end (ready ?a))
        (at start (not (at ?i ?l)))
        (at end (holding ?a ?i))
        (at start (not (load ?a ?c_pre)))
        (at end (load ?a ?c_post))
    )
  )

  (:durative-action pick_up_fragile
    :parameters (?a - obj ?i - obj ?p - obj ?l - location ?c_pre - count ?c_post - count)
    :duration (= ?duration 5)
    :condition (and
        (at start (ready ?a))
        (at start (at ?a ?l))
        (over all (at ?a ?l))
        (at start (at ?i ?l))
        (at start (load ?a ?c_pre))
        (over all (next ?c_pre ?c_post))
        (over all (capacity-ok ?a ?c_post))
        (at start (safe-location ?l))
        (over all (fragile ?i))
        (over all (holding ?a ?p))
        (at start (empty ?p))
        (at start (seismic-safe ?l))
    )
    :effect (and
        (at start (not (ready ?a)))
        (at end (ready ?a))
        (at start (not (at ?i ?l)))
        (at end (holding ?a ?i))
        (at start (not (load ?a ?c_pre)))
        (at end (load ?a ?c_post))
        (at end (not (movement-allowed ?a)))
    )
  )

  (:durative-action drop_secure
    :parameters (?a - obj ?i - obj ?l - location ?c_pre - count ?c_post - count)
    :duration (= ?duration 5)
    :condition (and
        (at start (ready ?a))
        (at start (at ?a ?l))
        (over all (at ?a ?l))
        (at start (holding ?a ?i))
        (at start (load ?a ?c_pre))
        (over all (next ?c_post ?c_pre))
        (over all (transportable ?i))
    )
    :effect (and
        (at start (not (ready ?a)))
        (at end (ready ?a))
        (at end (at ?i ?l))
        (at start (not (holding ?a ?i)))
        (at start (not (load ?a ?c_pre)))
        (at end (load ?a ?c_post))
    )
  )

  (:durative-action drop_fragile
    :parameters (?a - obj ?i - obj ?l - location ?c_pre - count ?c_post - count)
    :duration (= ?duration 5)
    :condition (and
        (at start (ready ?a))
        (at start (at ?a ?l))
        (over all (at ?a ?l))
        (at start (holding ?a ?i))
        (at start (load ?a ?c_pre))
        (over all (next ?c_post ?c_pre))
        (over all (fragile ?i))
    )
    :effect (and
        (at start (not (ready ?a)))
        (at end (ready ?a))
        (at end (at ?i ?l))
        (at start (not (holding ?a ?i)))
        (at start (not (load ?a ?c_pre)))
        (at end (load ?a ?c_post))
        (at end (movement-allowed ?a))
    )
  )

  (:durative-action load_pod
    :parameters (?a - obj ?art - obj ?pod - obj ?l - location ?c_pre - count ?c_post - count)
    :duration (= ?duration 5)
    :condition (and
        (at start (ready ?a))
        (at start (at ?a ?l))
        (over all (at ?a ?l))
        (over all (holding ?a ?pod))
        (at start (holding ?a ?art))
        (at start (empty ?pod))
        (at start (load ?a ?c_pre))
        (over all (next ?c_post ?c_pre))
        (at start (safe-location ?l))
    )
    :effect (and
        (at start (not (ready ?a)))
        (at end (ready ?a))
        (at start (not (holding ?a ?art)))
        (at start (not (empty ?pod)))
        (at end (inside ?art ?pod))
        (at start (not (load ?a ?c_pre)))
        (at end (load ?a ?c_post))
        (at end (movement-allowed ?a))
    )
  )

  (:durative-action unload_pod_secure
    :parameters (?a - obj ?art - obj ?pod - obj ?l - location ?c_pre - count ?c_post - count)
    :duration (= ?duration 5)
    :condition (and
        (at start (ready ?a))
        (at start (at ?a ?l))
        (over all (at ?a ?l))
        (over all (holding ?a ?pod))
        (at start (inside ?art ?pod))
        (at start (load ?a ?c_pre))
        (over all (next ?c_pre ?c_post))
        (over all (capacity-ok ?a ?c_post))
        (at start (safe-location ?l))
        (over all (transportable ?art))
    )
    :effect (and
        (at start (not (ready ?a)))
        (at end (ready ?a))
        (at end (holding ?a ?art))
        (at end (empty ?pod))
        (at start (not (inside ?art ?pod)))
        (at start (not (load ?a ?c_pre)))
        (at end (load ?a ?c_post))
    )
  )

  (:durative-action unload_pod_fragile
    :parameters (?a - obj ?art - obj ?pod - obj ?l - location ?c_pre - count ?c_post - count)
    :duration (= ?duration 5)
    :condition (and
        (at start (ready ?a))
        (at start (at ?a ?l))
        (over all (at ?a ?l))
        (over all (holding ?a ?pod))
        (at start (inside ?art ?pod))
        (at start (load ?a ?c_pre))
        (over all (next ?c_pre ?c_post))
        (over all (capacity-ok ?a ?c_post))
        (at start (safe-location ?l))
        (over all (fragile ?art))
    )
    :effect (and
        (at start (not (ready ?a)))
        (at end (ready ?a))
        (at end (holding ?a ?art))
        (at end (empty ?pod))
        (at start (not (inside ?art ?pod)))
        (at start (not (load ?a ?c_pre)))
        (at end (load ?a ?c_post))
        (at end (not (movement-allowed ?a)))
    )
  )

  ;; --- NUOVA AZIONE ---
  (:durative-action cool_down
    :parameters (?a - obj ?i - obj ?l - location)
    :duration (= ?duration 5)
    :condition (and
        (at start (ready ?a))
        (at start (at ?a ?l))
        (over all (at ?a ?l))
        (at start (holding ?a ?i))
        (over all (holding ?a ?i))
        (over all (has-cooling ?a))
        (at start (needs-cooling ?i))
    )
    :effect (and
        (at start (not (ready ?a)))
        (at end (ready ?a))
        (at end (not (needs-cooling ?i)))
    )
  )
)